#!/usr/bin/perl -w
#
# Author: Donovan Young
#
#  $Id$
#
# Description:
# -----------
#
# Nagios script to check CASS installation for freshness
#
# Implementation Notes:
# ---------------------
#
#  Requires $JCL_HOME env variable to be set
#  Dependancy on $JCL_HOME/getmdatadbdate
#
########################################################################
#
#  $Log: check_cass,v $
#  Revision 1.4  2007/06/25 16:12:25  dyoung
#  Corrected nagexec prototype error
#
#  Revision 1.3  2007/02/12 23:32:12  dyoung
#  Changed default response to UNKNOWN if -w or -c not supplied
#
#  Revision 1.2  2007/02/12 23:16:21  dyoung
#  Added check for expired database
#
#  Revision 1.1  2007/01/31 23:48:53  dyoung
#  Initial release
#
#
########################################################################

require 5.004;

use POSIX;
use strict;
use Getopt::Long;
use Time::Local;
use lib '/usr/local/nagios/libexec';
use vars qw($PROGNAME $CHECK_NAME $opt_w $opt_c $verbose);
use utils qw($TIMEOUT %ERRORS &print_revision &support &usage);

$PROGNAME  = 'check_cass';
$CHECK_NAME   = 'CASS';

Getopt::Long::Configure('bundling', 'no_ignore_case');
GetOptions
    ("V|version"     => \&version,
     "h|help"        => \&help,
     "v|verbose"     => \$verbose,
     "w|warning=i"   => \$opt_w,
     "c|critical=i"  => \$opt_c,
     );

#
# Subroutines
#
sub print_usage () {
    print "Usage: $PROGNAME [-vh] [-w warn] [-c crit]\n";
}

sub revision() {
    print_revision($PROGNAME,'$Revision: 1.4 $');
}

sub print_help () {
    revision();
    print "\nWritten by Donovan Young\n\nPerl CASS Status plugin for Nagios\n\n";
    print_usage();
    print "
-v, --verbose
   Return additional information. 
   Intended as a command-line aid, not recommended for Nagios script usage.
   
-w, --warning=INTEGER
   Level at which we should issue a warning.  If omitted, no warning is generated. 
   
-c, --critical=INTEGER
   Level at which we should issue a critical alert.  A critical is also generated
   if we cannot reach the StatusCheck webpage.

";
}

sub version() {
    revision();
    exit $ERRORS{'OK'};
}

sub help() {
    print_help();
    exit $ERRORS{'OK'};
}

sub nagios_exit($$) {
    my ( $state, $message ) = @_;
    printf "%s %s - %s\n", $CHECK_NAME, $state, $message;
    exit $ERRORS{$state};
}

# Initialize our variables and set our defaults
my $check_state  = 'UNKNOWN';
my $check_result = undef;
my $check_directory = '/usr/local/mdata';
my $check_days = undef;
my ( $release_year, $release_month, $release_day, $release_number ) = undef;
my ( $release_epoch, $release_date, $current_epoch, $current_date ) = undef;

# Make sure the symlink exists
if ( not -l $check_directory ) { nagios_exit( 'CRITICAL', "$check_directory symbolic link does not exist" ); }

# Make sure JCL_HOME is set
if ( not defined $ENV{'JCL_HOME'} ) { nagios_exit( 'CRITICAL', "JCL_HOME environment variable is not set" ); }
my $check_util = $ENV{'JCL_HOME'} . "/getmdatadbdate";

# Check for the existence of our db date utility
if ( not -x $check_util ) { nagios_exit( 'CRITICAL', "$check_util does not exist or is not executable" ); }

# Get the release date
$release_date = qx{$check_util 2>&1};

# Check for expiration
if ( $release_date =~ m/expired/ ) { nagios_exit ( 'CRITICAL', "CASS Data has expired" ); }

# Parse database date
( $release_year, $release_month, $release_day ) = $release_date =~ m/(\d{4})\-(\d{2})\-(\d{2})/;

# Get the release number
$release_number = readlink $check_directory;
( $release_number ) = $release_number =~ m/mdata.(\d{6})/;

# Convert current and release dates into epoch time
$current_epoch = timelocal(localtime);
$release_epoch = timelocal(0, 0, 0, $release_day, $release_month - 1, $release_year - 1900);

# Calculate the age in days
$check_days = ( gmtime($current_epoch - $release_epoch) )[7];

# Current and release date in MM/DD/YYYY format
$current_date = sprintf "%02d/%02d/%4d", (localtime)[4] + 1, (localtime)[3], (localtime)[5] + 1900;
$release_date = sprintf "%02d/%02d/%4d", $release_month, $release_day, $release_year;

# Status string
$check_result = "CASS Database $release_number is $check_days days old | " .
                "Current = $current_date; " .
                "Release = $release_date";

# Check for WARNING or CRITICAL conditions
if ( defined $opt_w or defined $opt_c ) {
    $check_state = 'OK';
    $check_state = 'WARNING'  if ( defined $opt_w && $check_days >= $opt_w );
    $check_state = 'CRITICAL' if ( defined $opt_c && $check_days >= $opt_c );
}

if ( $verbose ) {
    printf "Release Number    = $release_number\n";
    printf "Current Date      = $current_date\n";
    printf "Release Date      = $release_date\n";
    printf "Release Age       = %d days\n", $check_days;
    printf "Warning at        = $opt_w\n" if defined $opt_w;
    printf "Critical at       = $opt_c\n" if defined $opt_c;
    printf "Status            = $check_state\n";
}

# Exit based on current state
nagios_exit( $check_state, $check_result );

#======= end check_mda =====
